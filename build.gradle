plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4' apply false
    id 'org.spongepowered.plugin' version '0.9.0' apply false
    id 'fabric-loom' version '0.5-SNAPSHOT' apply false
    id 'org.cadixdev.licenser' version '0.5.0'
}

allprojects {
    apply plugin: 'org.cadixdev.licenser'

    group = 'io.github.boogiemonster1o1'
    version = '0.1.0'

    repositories {
        mavenCentral()
    }

    license {
        include '**/*.java'
        header = rootProject.file("HEADER")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    jar {
        from "LICENSE"
    }

    tasks.jar.dependsOn(licenseFormat)

    shadowJar {
        dependencies {
            include(dependency(':TimeHud-common'))
        }
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

task licenseFormatAll
subprojects { p -> licenseFormatAll.dependsOn("${p.path}:licenseFormat") }
